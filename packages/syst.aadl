package syst
public

-- We should define the system we'll be working with first
system SecuritySystem
end SecuritySystem;

-- For each system and device (system component), we must define an implementation
system implementation SecuritySystem.with_devices
	-- We must list all the devices declared bellow inside the 
	-- system using the "subcomponents" keyword
	subcomponents
		-- Each subcomponent (devices) of the system must have an inside name 
		-- which in this case is "this_motion_sensor"
		this_motion_sensor: device MotionSensor;
		this_magnetic_alarm_sensor: device MagneticAlarmSensor;
		this_camera: device Camera;
		this_alarm_unit: device AlarmUnit;
		
		-- Processes should also be added in the subcomponents session
		this_controller: process Controller;
		
		-- If there's a component in a different package that we need to declare
		-- in this section, we must do: this_component_name: device PackageName::DeviceName
		
	-- All connections in between ports are defined here (in the connections subsection)
	connections
		-- Format: [connection_name]: port [(device/controller)_name].[out_port_name] -> [(device/controller)_name].[in_port_name]  
		motion_conn: port this_motion_sensor.motion_detected -> this_controller.motion_in;
		magnetic_conn: port this_magnetic_alarm_sensor.alarm_set_off -> this_controller.magnetic_in;
		camera_conn_out: port this_controller.camera_out -> this_camera.camera_on_off;
		alaram_conn: port this_controller.alarm_out -> this_alarm_unit.alarm_on_off;
		camera_conn_in: port this_camera.video_stream -> this_controller.video_from_camera;
end SecuritySystem.with_devices;

-- Process is a piece a Software in the system. In this case, our process processes the
-- data from the devices connected to it
process Controller
	features
		motion_in: in event port;
		magnetic_in: in event port;
		camera_out: out event port;
		alarm_out: out event port;
		video_from_camera: in data port;
end Controller;

process implementation Controller.with_threads
end Controller.with_threads;

-- After declaring the system and its implementation, we must declare all the 
-- system's components. We must also declare an implementation for each declared component.
device MotionSensor
	features
		-- Device ports should be added in the "features" section of each device
		motion_detected: out event port;
end MotionSensor;

device implementation MotionSensor.impl
end MotionSensor.impl;

device MagneticAlarmSensor
	features
		alarm_set_off: out event port;
end MagneticAlarmSensor;

device implementation MagneticAlarmSensor.impl
end MagneticAlarmSensor.impl;

device Camera
	features
		camera_on_off: in event port;
		video_stream: out data port;
end Camera;

device implementation Camera.impl
end Camera.impl;

device AlarmUnit
	features
		alarm_on_off: in event port;
end AlarmUnit;

device implementation AlarmUnit.impl
end AlarmUnit.impl;

end syst;